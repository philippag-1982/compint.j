defaultTasks 'compileJava', 'compileTestJava'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.jmh:jmh-gradle-plugin:0.7.2"
    }
}

apply plugin: "me.champeau.jmh"
apply plugin: 'java-library'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.2'
}

sourceSets.test.java.srcDirs += 'src/perf/java'
task 'perf' (type: Test)

tasks.withType(Test) {
    if ("test".equals(it.name)) {
        jvmArgs '-ea'
        exclude '**/*Performance.class'
    } else if ("perf".equals(it.name)) {
        jvmArgs '-da', '-Xmx8G'
        exclude '**/*Test.class'
        testLogging.showStandardStreams = true
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Werror');
    options.compilerArgs.add('-Xlint:all');
}

jmh {
    failOnError.set(true)
    profilers.add("gc")
}
